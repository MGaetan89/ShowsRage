apply plugin: 'jacoco'
apply plugin: 'com.android.application'
apply plugin: 'com.github.kt3k.coveralls'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'realm-android'

def properties = new Properties()

if (rootProject.file('local.properties').exists()) {
	properties.load(rootProject.file('local.properties').newDataInputStream())
}

android {
	compileSdkVersion 25
	buildToolsVersion '25.0.1'

	defaultConfig {
		applicationId 'com.mgaetan89.showsrage'
		minSdkVersion 15
		targetSdkVersion 25
		versionCode 28
		versionName '1.5'
		resConfigs 'en', 'fr'
		testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
		vectorDrawables.useSupportLibrary = true
	}

	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_7
		targetCompatibility JavaVersion.VERSION_1_7
	}

	signingConfigs {
		release {
			keyAlias properties.getProperty('signing.keyAlias', '')
			keyPassword properties.getProperty('signing.keyPassword', '')
			storeFile file(properties.getProperty('signing.storeFile', '-'))
			storePassword properties.getProperty('signing.storePassword', '')
		}
	}

	buildTypes {
		debug {
			applicationIdSuffix '.dev'
			testCoverageEnabled true
		}

		release {
			minifyEnabled false

			if (file(signingConfigs.release.storeFile).exists()) {
				signingConfig signingConfigs.release
			}
		}
	}

	sourceSets {
		androidTest.java.srcDirs = ['src/androidTest/kotlin']
		main.java.srcDirs = ['src/main/kotlin']
		test.java.srcDirs = ['src/test/kotlin']
	}

	dataBinding {
		enabled = true
	}

	lintOptions {
		abortOnError false
	}

	testOptions {
		unitTests {
			returnDefaultValues = true
		}
	}
}

tasks.withType(Test) {
	testLogging {
		exceptionFormat 'full'
	}
}

// From https://github.com/nenick/android-gradle-template/blob/master/app/build.jacoco-test-report.gradle
task jacocoTestReport(type: JacocoReport /*, dependsOn: ['test'] */) {
	group = 'Reporting'
	description = 'Generates Jacoco coverage reports: XML and HTML'
	//outputs.upToDateWhen { false }

	// use hidden configuration, for details look into JacocoPlugin.groovy
	jacocoClasspath = configurations['androidJacocoAnt']

	// exclude auto-generated classes and tests
	def fileFilter = ['**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*', '**/*Test*.*', 'android/**/*.*']
	def debugTree = fileTree(dir: "${buildDir}/intermediates/classes/debug", excludes: fileFilter)
	def mainSrc = "${projectDir}/src/main/kotlin"

	sourceDirectories = files([mainSrc])
	classDirectories = files([debugTree])
	executionData = fileTree(dir: projectDir, includes: ['**/*.exec', '**/*.ec'])

	reports {
		csv {
			enabled = false
		}

		html {
			enabled = true
			destination = "${buildDir}/reports/jacoco"
		}

		xml {
			enabled = true
			destination = "${buildDir}/reports/jacoco/test/jacocoTestReport.xml"
		}
	}
}

def assertJAndroidVersion = '1.1.1'
def googleServicesVersion = '9.8.0'
def jUnitVersion = '4.12'
def kolumbusVersion = 'v0.9'
def okHttpVersion = '2.5.0'
def supportLibraryVersion = '25.0.1'
def supportTestVersion = '0.5'

dependencies {
	compile "com.android.support:appcompat-v7:${supportLibraryVersion}"
	compile "com.android.support:cardview-v7:${supportLibraryVersion}"
	compile "com.android.support:customtabs:${supportLibraryVersion}"
	compile "com.android.support:design:${supportLibraryVersion}"
	compile "com.android.support:mediarouter-v7:${supportLibraryVersion}"
	compile "com.android.support:palette-v7:${supportLibraryVersion}"
	compile "com.android.support:recyclerview-v7:${supportLibraryVersion}"
	compile "com.android.support:support-annotations:${supportLibraryVersion}"
	compile "com.android.support:support-v4:${supportLibraryVersion}"
	compile "com.android.support:support-vector-drawable:${supportLibraryVersion}"
	compile 'com.futuremind.recyclerfastscroll:fastscroll:0.2.4'
	compile 'com.github.bumptech.glide:glide:3.7.0'
	compile 'com.github.bumptech.glide:okhttp-integration:1.4.0'
	compile "com.google.android.gms:play-services-cast:${googleServicesVersion}"
	compile "com.google.firebase:firebase-analytics:${googleServicesVersion}"
	compile "com.google.firebase:firebase-crash:${googleServicesVersion}"
	compile "com.squareup.okhttp:okhttp:${okHttpVersion}"
	compile "com.squareup.okhttp:okhttp-urlconnection:${okHttpVersion}"
	compile 'com.squareup.retrofit:retrofit:1.9.0'
	compile 'com.takisoft.fix:preference-v7:25.0.1.0'
	compile 'jp.wasabeef:glide-transformations:2.0.1'
	compile 'org.jetbrains.kotlin:kotlin-stdlib:1.0.5-2'

	debugCompile "com.github.MGaetan89.Kolumbus:kolumbus:${kolumbusVersion}"
	releaseCompile "com.github.MGaetan89.Kolumbus:kolumbus-no-op:${kolumbusVersion}"

	testCompile("com.squareup.assertj:assertj-android:${assertJAndroidVersion}") {
		exclude group: 'com.android.support', module: 'support-annotations'
	}
	testCompile "junit:junit:${jUnitVersion}"
	testCompile 'org.mockito:mockito-core:2.2.25'

	androidTestCompile "com.android.support:support-annotations:${supportLibraryVersion}"
	androidTestCompile "com.android.support.test:rules:${supportTestVersion}"
	androidTestCompile "com.android.support.test:runner:${supportTestVersion}"
	androidTestCompile("com.squareup.assertj:assertj-android:${assertJAndroidVersion}") {
		exclude group: 'com.android.support', module: 'support-annotations'
	}
	androidTestCompile "junit:junit:${jUnitVersion}"
}

apply plugin: 'com.google.gms.google-services'
